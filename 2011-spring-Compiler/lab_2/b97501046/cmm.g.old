Program				:	BlockStmt
					;
BlockStmt			:	"{"	VarDecl	Stmt	"}"
					;
VarDecl				:	int	VarDeclNum	id	VarDeclId	";"	VarDecl
					|
					;
VarDeclNum			:	"["	num	"]"	VarDeclNum
					|
					;
VarDeclId			:	","	id	VarDeclId
					|
					;
Var					:	id	VarArithExpr
					;
VarArithExpr		:	"["	ArithExpr	"]" VarArithExpr
					|
					;
Stmt				:	Var	"="	ArithExpr	";"	Stmt
					|	"if"	"("	LogicExpr	")"	BlockStmt	StmtElse	Stmt
					|	"while"	"("	LogicExpr	")"	BlockStmt	Stmt
					|	"break"	";"	Stmt
					|	"continue"	";"	Stmt
					|	"scan"	"("	Var	StmtVar	")"	";"	Stmt
					|	"print"	"("	PrintableExpr	StmtPrintableExpr	")"	";"	Stmt
					|	BlockStmt	Stmt
					|
					;
StmtElse			:	"else"	BlockStmt
					|
					;
StmtVar				:	","	Var	StmtVar
					|
					;
StmtPrintableExpr	:	","	PrintableExpr	StmtPrintableExpr
					|
					;
PrintableExpr		:	string
					|	ArithExpr
					;
ArithExpr			:	ArithExprMD	ArithExprASMore
					|	"+"	ArithExpr
					|	"-"	ArithExpr
					;
ArithExprMD			:	ArithExprBase	ArithExprMDMore
					;
ArithExprASMore		:	"+"	ArithExprMD	ArithExprASMore
					|	"-"	ArithExprMD	ArithExprASMore
					|
					;
ArithExprBase		:	num
					|	Var
					|	"("	ArithExpr	")"
					;
ArithExprMDMore		:	"*"	ArithExprBase	ArithExprMDMore
					|	"/"	ArithExprBase	ArithExprMDMore
					|
					;
LogicExpr			:	LogicExprAnd	LogicExprOrMore
					|	"!"	LogicExpr
					;
LogicExprAnd		:	LogicExprBase	LogicExprAndMore
                	;
LogicExprOrMore		:	"||"	LogicExprAnd	LogicExprOrMore
					|
					;
LogicExprBase		:	ArithExpr	LogicExprComp
					|	"["	LogicExpr	"]"
					;
LogicExprAndMore	:	"&&"	LogicExprBase	LogicExprAndMore
					|
					;
LogicExprComp		:	">"		ArithExpr
					|	">="	ArithExpr
					|	"<"		ArithExpr
					|	"<="	ArithExpr
					|	"=="	ArithExpr
					|	"!="	ArithExpr
					;
